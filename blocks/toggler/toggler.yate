func nb-toggler(nodeset toggler) {
    default = {
        'leftText': 'On'
        'rightText': 'Off'
        'size': 's'
        //'disabled': true()
        //'checked': true()
    }

    nb-block('toggler', nb-extend(default, toggler))
}

match .toggler nb {
    <a tabindex="1" class="nb-toggler nb-toggler_size_{.size} _init" data-nb="toggler">
        apply . nb-main-attrs
        if .checked {
            @class += ' nb-toggler_checked'
        }
        if .disabled {
            @class += ' is-nb-disabled'
        }
        <label class="nb-toggler__label">
            <input type="checkbox" class="nb-toggler__checkbox" >
                if .attrs.name {
                    @name = .attrs.name
                }

                if .checked {
                    @checked = 'checked'
                }

                if .disabled {
                    @disabled = 'disabled'
                }
            </input>
        </label>
        <div class="nb-toggler__container">
            <div class="nb-toggler__sticker nb-toggler__sticker_left">
                <span class="nb-toggler__text">
                    .leftText
                </span>
            </div>
            <div class="nb-toggler__sticker nb-toggler__sticker_right">
                <span class="nb-toggler__text">
                    .rightText
                </span>
            </div>
        </div>
        <div class="nb-toggler__knob"></div>
    </a>
}
