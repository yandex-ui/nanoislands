func nb-suggest(nodeset options) {
    defaults = {
        'countMax': '10'
        'type': 'default'
    }

    _nb-block('suggest', _nb-extend(defaults, options))
}

match .suggest nb {
    <div class="nb-suggest _init" data-nb="suggest">
        @data-countMax = .countMax
        @data-type = .type
        @data-size = .size
        @data-source = .source
        @data-highlight = .highlight
        @data-class-suggest = 'nb-island _nb-fly-island _nb-suggest-container _nb-{.size}-suggest'


        if .class {
            @data-class-suggest += ' {.classSuggest}'
        }

        apply . nb-main-attrs

        if .disabled  {
          @class += ' _nb-is-disabled'
        }

        nb-input({
             'size': .size
             'class': '_nb-suggest-input'
             'attrs': .attrsInput
             'tabindex': .tabindex
             'disabled': .disabled
             'name': .name
             'content': .content
         })
    </div>
}

match / nb-suggest {
    <li class="_nb-suggest-item">
        <a class="_nb-suggest-item-link">
            apply .item nb-suggest
        </a>
    </li>
}

match /[.type == 'default'].item nb-suggest {
    if .labelContent {
        html(.labelContent)
    }
}

match /[.type == 'username'].item nb-suggest {
    user = {
        'size': /.size
        'mod': 'suggest'
        'username': (
            if .username {
                .username
            } else if .email {
                .email
            }
        )
        'rightToLeft': true()
        if .username && .email {
            'email': .email
        }
        'justify': true()
        if .userpic {
            'userpic': .userpic
        }
        if .usernameHighlighted {
            'usernameHighlighted': .usernameHighlighted
        }
        if .emailHighlighted {
            'emailHighlighted': .emailHighlighted
        }
    }

    nb-user(user)
}

match .user[.mod == 'suggest' && .usernameHighlighted].username nb-user-name {
    html(..usernameHighlighted)
}

match .user[.mod == 'suggest' && .emailHighlighted].email nb-user-name {
    html(..emailHighlighted)
}
