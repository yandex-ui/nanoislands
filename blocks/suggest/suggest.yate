func nb-suggest(nodeset options) {
    defaults = {
        'countMax': '10'
        'type': 'default'
    }

    nb-block('suggest', nb-extend(defaults, options))
}

match .suggest nb {
    <div class="nb-suggest _init" data-nb="suggest">
        @data-countMax = .countMax
        @data-type = .type
        @data-size = .size
        @data-source = .source
        @data-highlight = .highlight
        @data-class-suggest = 'nb-island nb-island_type_fly nb-suggest__container nb-suggest_size_{.size}'
        if .class {
            @data-class-suggest += ' {.classSuggest}'
        }

        apply . nb-main-attrs

        if .disabled  {
          @class += ' nb-is-disabled'
        }

        nb-input({
             'size': .size
             'class': 'nb-suggest__input'
             'attrs': .attrsInput
             'disabled': .disabled
             'name': .name
             'content': .content
         })
    </div>
}

match / nb-suggest-item {
    <li class="nb-suggest__item">
        apply . nb-suggest-item-content
    </li>
}

match /[.type == 'default' || .type == 'username'] nb-suggest-item-content {
    <a class="nb-suggest__item__link">
        apply .item nb-suggest-item-content
    </a>
}

match /[.type == 'default'].item nb-suggest-item-content {
    if .labelContent {
        html(.labelContent)
    }
}

match /[.type == 'username'].item nb-suggest-item-content {
    user = {
        'size': /.size
        'mod': 'suggest'
        'username': (
            if .username {
                .username
            } else if .email {
                .email
            }
        )
        'rightToLeft': true()
        if .username && .email {
            'email': .email
        }
        'justify': true()
        if .userpic {
            'userpic': .userpic
        }
        if .usernameHighlighted {
            'usernameHighlighted': .usernameHighlighted
        }
        if .emailHighlighted {
            'emailHighlighted': .emailHighlighted
        }
    }

    nb-user(user)
}

match .user[.mod == 'suggest' && .usernameHighlighted].username nb-user-name {
    html(..usernameHighlighted)
}

match .user[.mod == 'suggest' && .emailHighlighted].email nb-user-name {
    html(..emailHighlighted)
}
