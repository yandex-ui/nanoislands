func nb-radio-button(nodeset options) {
    default = {
        'size': 'm'
        'theme': 'normal'
        'type': 'radio'
    }

    nb-block('radio-button', nb-extend(default, options))
}

match .radio-button nb {
    <div data-nb='radio-button' class='nb-radio-button _init'>
        apply . nb-main-attrs
        for .group {

            uniq = 'nb-radio-button_{nb-uniq()}'
            <span class="nb-radio-button__button">
                <input class="nb-radio-button__radio" type="radio" name="{ ..name }" value="{ .value }" id="{ uniq }">
                    if .checked {
                        @checked = 'checked'
                    }
                    if .disabled {
                        @disabled = 'disabled'
                    }
                </input>

                <label for="{ uniq }" class = 'nb-button  js-button' data-value='{ .value }'>
                    apply . nb-main-attrs

                    if ..theme == 'normal' {
                        if ..size != 's' {
                            @class += ' nb-normal-button'
                        } else {
                            @class += ' nb-small-button'
                        }
                    } else {
                        if ..size != 's' {
                            @class += ' nb-{ ..theme }-button'
                        } else {
                            @class += ' nb-small-{ ..theme }-button'
                        }
                    }

                    if .checked {
                        @class += ' nb-button_type_checked'
                    }

                    if .disabled {
                        @class += ' is-nb-disabled js-disabled'
                        @disabled = 'disabled'
                    }

                    if !(.content) && .icon {
                        @class += ' nb-button_only-icon'
                    }

                    if .type == 'round' {
                        @class += " nb-button_type_round"
                    }

                    <span class="nb-button-content">
                        if (.content && .icon) {
                            apply .icon nb
                            html(.content)
                        } else if (!.content && .icon){
                            apply .icon nb
                        } else {
                            html(.content)
                        }
                    </span>
                </label>
            </span>

        }

    </div>
}
