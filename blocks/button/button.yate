func nb-button(nodeset options) {
    default = {
        'size': 'm'
        'theme': 'normal'
    }

    if options.type == 'file' {
        nb-block('buttonAttach', nb-extend(default, options))
    } else if options.href {
        nb-block('buttonLink', nb-extend(default, options))
    } else {
        nb-block('button', nb-extend(default, options))
    }
}

func _nb-button-attributes() {
    if !( .static ) {
        @data-nb = 'button'
    }

    if .disabled {
        @class += ' is-nb-disabled'
    }

    // FIXME: можно ли как-то сделать тут переменную, хранящую `-small` если .size == 's'
    //        и пустую строку в противном случае?
    if .theme != 'normal' {
        if .size != 's' {
            @class += ' nb-{ .theme }-button'
        } else {
            @class += ' nb-small-{ .theme }-button'
        }
    } else {
        if .size != 's' {
            @class += ' nb-button'
        } else {
            @class += ' nb-small-button'
        }
    }

    if !( .content ) && .icon {
        @class += ' nb-button_only-icon'
    }

    // TODO: Как перенести сюда nb-button-content? Ну и `apply . nb-main-attrs`, раз уж на то пошло?
}

match .button nb {
    <button class="_init">
        apply . nb-main-attrs

        _nb-button-attributes()

        if .disabled {
            @disabled = 'disabled'
        }

        <span class="nb-button-content">
            if (.content && .icon) {
                apply .icon nb
                html(.content)
            } else if (!.content && .icon){
                apply .icon nb
            } else {
                html(.content)
            }
        </span>
    </button>
}

match .buttonLink nb {
    <a class="_init">

        apply . nb-main-attrs

        _nb-button-attributes()

        if !( .disabled ) && .href {
            @href="{ .href }"
        }

        <span class="nb-button-content">
            if (.content && .icon) {
                apply .icon nb
                html(.content)
            } else if (!.content && .icon ){
                apply .icon nb
            } else {
                html(.content)
            }
        </span>
    </a>
}

match .buttonAttach nb {
    <label class="_init">

        apply . nb-main-attrs

        _nb-button-attributes()

        <span class="nb-file-intruder">
            <span class="nb-file-intruder-inner">
                <input class="nb-file-intruder-input" type="file">
                    if .multiple {
                        @multiple = "multiple"
                    }
                    if .attrs.name {
                        @name = "{.attrs.name}"
                    }
                    if .disabled {
                        @disabled = 'disabled'
                    }
                </input>
                <span class="nb-file-intruder-focus"></span>
            </span>
        </span>
        <span class="nb-button-content">
            if (.content && .icon) {
                apply .icon nb
                html(.content)
            } else if (!.content && .icon){
                apply .icon nb
            } else {
                html(.content)
            }
        </span>
    </label>
}
