func nb-button(nodeset options) {
    default = {
        'size': 'm'
        'theme': 'normal'
    }

    if options.type == 'file' {
        nb-block('buttonAttach', nb-extend(default, options))
    } else if options.href {
        nb-block('buttonLink', nb-extend(default, options))
    } else {
        nb-block('button', nb-extend(default, options))
    }
}

match .button nb {
    <button class="nb-button _init nb-button_size_{ .size }  nb-button_theme_{ .theme }">
        if !( .static ) {
            @data-nb = 'button'
        }

        apply . nb-main-attrs

        if .disabled {
            @class += ' nb-button_disabled'
            @disabled = 'disabled'
        }

        if !( .content ) && .icon {
            @class += ' nb-button_only-icon'
        }

        <span class="nb-button__text">
            if (.content && .icon) {
                apply .icon nb
                html(.content)
            } else if (!.content && .icon){
                html('&nbsp;')
                apply .icon nb
            } else {
                html(.content)
            }
        </span>
    </button>
}

match .buttonLink nb {
    <a class="nb-button _init nb-button_size_{ .size }  nb-button_theme_{ .theme }">
        apply . nb-main-attrs

        if .disabled {
            @class += ' nb-button_disabled'
            @disabled = 'disabled'
        }

        if !( .disabled ) && .href {
            @href="{ .href }"
        }

        if !( .static ) {
            @data-nb = 'button'
        }

        if !( .content ) && .icon {
            @class += ' nb-button_only-icon'
        }

        <span class="nb-button__text">
            if (.content && .icon) {
                apply .icon nb
                html(.content)
            } else if (!.content && .icon ){
                html('&nbsp;')
                apply .icon nb
            } else {
                html(.content)
            }
        </span>
    </a>
}

match .buttonAttach nb {
    <span class="nb-button _init nb-button_size_{ .size } nb-button_type_attach nb-button_theme_{ .theme }">
        apply . nb-main-attrs

        if .disabled {
            @class += ' nb-button_disabled'
            @disabled = 'disabled'
        }

        if !( .disabled ) && .href {
            @href="{ .href }"
        }

        if !( .static ) {
            @data-nb = 'button'
        }

        if !( .content ) && .icon {
            @class += ' nb-button_only-icon'
        }

        <span class="nb-button__text">
            if (.content && .icon) {
                apply .icon nb
                html(.content)
            } else if (!.content && .icon){
                html('&nbsp;')
                apply .icon nb
            } else {
                html(.content)
            }
        </span>
        <input type="file" class="nb-button__attach"></input>
    </span>
}



