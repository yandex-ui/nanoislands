func nb-button(nodeset options) {
    default = {
        'size': 'm'
        'theme': 'normal'
    }

    if options.type == 'file' {
        _nb-block('buttonAttach', _nb-extend(default, options))
    } else if options.type == 'inline' {
        _nb-block('buttonSpan', _nb-extend(default, options))
    } else if options.type == 'label' {
        _nb-block('buttonLabel', _nb-extend(default, options))
    } else if options.type == 'link' {
        _nb-block('buttonLink', _nb-extend(default, options))
    }  else {
        _nb-block('button', _nb-extend(default, options))
    }
}

func _nb-button-attributes(type) {

    _size-theme-type-class({
       'name': 'button'
       'size': .size
       'theme': .theme
    })

    if .disabled {
        @class += ' _nb-is-disabled'
        @tabindex = '-1'

        if type == 'button' {
            @disabled = 'disabled'
        }
    }

    if .tabindex {
        @tabindex = .tabindex
    }

    if type == 'attach' {
        @class += ' _nb-attach-button'
    }

    if .checked {
        @class += ' is-checked'
    }

    if type == 'button' {
        if .name {
            @name = .name
        }

        if .attrs.name {
            @name = .attrs.name
        }
    }

    if !( .content ) && (.icon || .iconText) {
        @class += ' _nb-with-only-button'
    }


}

func _nb-button-content() {
    if .iconText {
        <span class="nb-icon _nb-icon-text">
          .iconText
        </span>
    }
    if .content {
        html(.content)
    }
}

func _nb-button-static(){
    if !( .static ) {
        @class += ' _init'
        @data-nb = 'button'
    }
}

match .button nb {
    <button>
        _nb-button-attributes('button')

        _nb-button-static()

        apply . nb-main-attrs

        <span class="_nb-button-content">
             if .icon {
                nb-icon({
                    'icon': .icon
                })
            }

            _nb-button-content()
        </span>
    </button>
}

match .buttonLink nb {
    <a>
        _nb-button-attributes('link')

         _nb-button-static()

        apply . nb-main-attrs

        <span class="_nb-button-content">
             if .icon {
                nb-icon({
                    'icon': .icon
                })
            }

            _nb-button-content()
        </span>
    </a>
}

match .buttonSpan nb {
    <span>
        _nb-button-attributes('span')

        _nb-button-static()

        apply . nb-main-attrs

        <span class="_nb-button-content">
            if .icon {
                nb-icon({
                    'icon': .icon
                })
            }

            _nb-button-content()
        </span>
    </span>
}

match .buttonLabel nb {
    <label>
        _nb-button-attributes('label')

        _nb-button-static()

        apply . nb-main-attrs

        <span class="_nb-button-content">
            if .icon {
                nb-icon({
                    'icon': .icon
                })
            }
            _nb-button-content()
        </span>
    </label>
}

match .buttonAttach nb {
    <label>
        _nb-button-attributes('attach')

        _nb-button-static()

        apply . nb-main-attrs

        <span class="_nb-file-intruder">
            <span class="_nb-file-intruder-inner">
                <input class="_nb-file-intruder-input" type="file">
                    if .attrs.multiple || .multiple {
                        @multiple = "multiple"
                    }

                    if .name {
                        @name = .name
                    }

                    if .attrs.name {
                        @name = "{.attrs.name}"
                    }
                    if .attrs.title {
                        @title += "{.attrs.title}"
                    }

                    if .disabled {
                        @disabled = 'disabled'
                    }
                </input>
                <span class="_nb-file-intruder-focus"></span>
            </span>
        </span>
        <span class="_nb-button-content">
            if .icon {
               nb-icon({
                   'icon': .icon
               })
           }

            _nb-button-content()
        </span>
    </label>
}
