
func nb-arrow(nodeset arrow) {
    default = {
        'search': 'true'
        'buttonContent': 'Find'
    }

    _nb-block('arrow', _nb-extend(default, arrow))
}

match .arrow nb {
    <form data-nb='arrow' class="nb-arrow _init _nb-search-arrow">

        apply . nb-main-attrs

        <span class="_nb-arrow-tail"></span>

        input-label = if .text {
            '<a href="' + .href + '" class="_nb-search-arrow-label">' + .text + '</a>'
        } else {
            ''
        }

        input-requests = if .requests {
            ' — ' + .requests
        } else {
            ''
        }

        nb-button({
            'size': 'm'
            'class': '_nb-search-arrow-button'
            'tabindex': .buttonTabindex
            'content': .buttonContent
            'attrs': {
                'type': 'submit'
            }
        })

        <div class="_nb-search-arrow-content">
            nb-input({
                'size': 'm'
                'class': '_nb-search-arrow-input'
                'tabindex': .inputTabindex
                'leftContent': input-label
                'attrs': {
                    'autocomplete': 'off'
                    'value': .value
                    'name': 'text'
                }
                'hint': {
                    'text': input-requests
                    'ghost': true()
                }
            })
        </div>

    </form>
}

match .arrow[.search != 'true'] nb {
    <div class="_nb-service-arrow">

        apply . nb-main-attrs

        <span class="_nb-arrow-tail"></span>

        .text
    </div>
}

match .arrow[.search != 'true'][.href] nb {
    <a href="{.href}" class="_nb-service-arrow">

        apply . nb-main-attrs

        <span class="_nb-arrow-tail"></span>

        .text
    </a>
}
