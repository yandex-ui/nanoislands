func nb-user(nodeset user) {
    default = {
        'size': 'm'
    }

    _nb-block('user', _nb-extend(default, user))
}

match .user nb {

    <span>
          _size-theme-type-class({
                'name': 'user'
                'size': .size
            })
            @class += " _init"

            if .justify {
                @class += " _nb-user_justify"
            }

        apply . nb-main-attrs


        if !.rightToLeft {
            @class += ' _nb-user_ltr'
        } else {
            @class += ' _nb-user_rtl'
        }

        apply . nb-user-pic
        apply . nb-user-name
    </span>
}

match .user nb-user-pic {
    <span >
        _size-theme-type-class({
            'name': 'user-avatar'
            'size': .size
        })

        if .userpic {
            @style+= " background-image: url('{.userpic}')"
        }

        if .notices > 0 {
            <span class="_nb-user-notice-count">
                .notices
            </span>
        }
    </span>
}

match .user[!!.username || !!.email] nb-user-name {
    <span class="_nb-user-label">
        <span class="_nb-user-name _link" title="{.username}">
            .username
        </span>
        if .email {
            <span class="_nb-user-email" title="{.email}">
               .email
            </span>
        }
    </span>
}
