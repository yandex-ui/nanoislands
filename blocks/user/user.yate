func nb-user(nodeset user) {
    default = {
        'size': 'm'
    }

    nb-block('user', nb-extend(default, user))
}

match .user nb {

    <span data-nb="user">
          _size-theme-type-class({
                'name': 'user'
                'size': .size
            })
            @class += " _init"

            if .justify {
                @class += " nb-user_justify"
            }

        apply . nb-main-attrs


        if !.rightToLeft {
            @class += ' nb-user_ltr'
            apply . nb-user-pic
            apply . nb-user-name
        } else {
            @class += ' nb-user_rtl'
            apply . nb-user-name
            apply . nb-user-pic
        }
    </span>
}

match .user nb-user-pic {
    <span >
        _size-theme-type-class({
            'name': 'user-avatar'
            'size': .size
        })

        if .userpic {
            @style+= " background-image: url('{.userpic}')"
        }

        if .notices > 0 {
            <span class="nb-user-notice-count">
                .notices
            </span>
        }
    </span>
}

match .user nb-user-name {
    <span class="nb-user-label">
        <span class="nb-user-name _link" title="{.username}">
            .username
        </span>
        if .email {
            <span class="nb-user-email" title="{.email}">
               .email
            </span>
        }
    </span>
}
