func nb-popup-menu(nodeset options) {
    default = {
       'none': 'none'
       //'tail': 'left|right|top|bottom'
       //'menu': [
       //    {
       //        'href': '#'
       //        'text': 'Скопировать'
       //     }
       //]
    }

    nb-block('popupMenu', nb-extend(default, options))
}

match .popupMenu nb {
    <div class="nb-popup _init">
        if !.static {
            @data-nb = "popup"
            @class += " nb-is-hidden"
        }

        apply . nb-main-attrs

        if .tail {
            @data-nb-tail = .tail
        }

        <ul class="nb-popup-menu">
            for .menu {
                if .separator {
                    <li class="nb-popup-separator"></li>
                } else {
                   <li class="nb-popup-line">
                        <a class="nb-popup-link">
                            if .href {
                                @href = .href
                            }

                            apply . nb-main-attrs
                            html(.content)
                        </a>
                    </li>
                }
            }
        </ul>
    </div>
}


func nb-popup(nodeset options) {
    default = {
       'none': 'none'
    }

    nb-block('popup', nb-extend(default, options))
}

match .popup nb {
    <div class="nb-popup _init nb-is-hidden" data-nb='popup'>

        apply . nb-main-attrs

        if .tail {
            @data-nb-tail = .tail
        }

        if .titleContent {
           _nb-popup-title(.titleContent)
        }

        if .content {
           _nb-popup-content(.content)
        }

    </div>
}

func nb-popup-modal(nodeset options) {
    default = {
        'data-nb': {
            'modal': 'true'
            'width': 'auto'
            'height': 'auto'
            'minHeight': 'auto'
        }
        // if there is privitive deep-extend with options don't work
        'close': {
            'none':'none'
        }
    }

    nb-block('modalPopup', nb-deep-extend(default, options))
}

match .modalPopup nb {
    if .static {
        nb-paranja()
    }
    <div>
        _size-theme-type-class({
           'name': 'popup'
           'theme': .theme
           'type': 'modal'
        })
        @class += ' _init'

        if !.static {
            @data-nb = "popup"
            @class += " nb-is-hidden"
        }

        apply . nb-main-attrs

        <div class="nb-popup-i">
            if .close {
                <a class="nb-popup-close { .close.class }">
                    if .close.attrs {
                        for .close.attrs.* {
                            @{name()} = .
                        }
                    }
                </a>
            }

            if .titleContent {
                _nb-popup-title(.titleContent)
            }

            if .content {
                _nb-popup-content(.content)
            }

            if .buttons {
                _nb-popup-buttons(.buttons)
            }

        </div>
    </div>
}


func _nb-popup-content(nodeset content) {
    <div class="nb-popup-content">
        html(content)
    </div>
}

func _nb-popup-title(nodeset title) {
    <div class="nb-popup-title">
        html(title)
    </div>
}


func _nb-popup-buttons(nodeset buttons) {
    <div class="nb-popup-buttons">
        apply buttons nb-main-attrs

        if count(buttons.data.*) {
           for buttons.data {
               nb-button(.)
           }
        } else {
           html(buttons.data)
        }
    </div>
}
