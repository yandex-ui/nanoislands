func nb-checkbox(nodeset options) {
    default = {
        'size': 'm'
        'type': 'checkbox'
    }

    nb-block('checkbox', nb-extend(default, options))
}

match .checkbox nb {
    uniq = if .id {
        .id
    } else {
        nb-uniq()
    }

    <label data-nb='checkbox' class="nb-checkbox nb-checkbox_size_{ .size } nb-checkbox_type_{ .type } _init" for="nb-checkbox_{ uniq }">
        apply . nb-main-attrs

        if .disabled  {
             @class += ' is-disabled'
        }


        <input class="nb-checkbox__input" type="{ .type }" id="nb-checkbox_{ uniq }">
            if .name {
                @name = .name
            }

            if .attrs.name {
                @name = .attrs.name
            }

            if .value {
                @value = .value
            }

            if .disabled  {
                 @disabled = 'disabled'
            }

            if .checked {
                @checked = 'checked'
            }
        </input>
        <span class="nb-checkbox__flag nb-checkbox__flag_type_{ .type }"><span class="nb-checkbox__flag__icon"></span></span>

        <span class="nb-checkbox__label">
            if (.content) {
                html(.content)
            }
        </span>
    </label>
}

