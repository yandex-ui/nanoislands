func nb-checkbox(nodeset options) {
    default = {
        'size': 'm'
        'type': 'checkbox'
        'theme': 'normal'
    }

    nb-block('checkbox', nb-extend(default, options))
}

match .checkbox nb {
    uniq = if .id {
        .id
    } else {
        nb-uniq()
    }

    <label data-nb='checkbox'  for="nb-checkbox_{ uniq }">

        apply . nb-main-attrs

          _size-theme-type-class({
                'name': 'checkbox'
                'size': .size
                if .type {
                    'type': .type
                }
             })
        @class += " _init"

        if .disabled  {
             @class += ' nb-is-disabled'
        }


        <input class="nb-checkbox-input" id="nb-checkbox_{ uniq }">
            if .type == 'button' {
                @type = 'checkbox'
            } else {
                @type = .type
            }

            if .name {
                @name = .name
            }

            if .tabindex {
                @tabindex = .tabindex
            }

            if .disabled  {
                 @disabled = 'disabled'
            }

            if .checked {
                @checked = 'checked'
            }

            // бесмысленная и беспощадная проверка на наличие свойства value
            // потому что пустое value и его отсутствие - не одно и то же
            if count(.value) {
                @value = .value
            }

        </input>

        if .type == 'button' {
            nb-button(nb-extend(., {
                'type': 'inline'
                'static': true()
                'id': false()
                'attrs': false()
                'class': [
                    'nb-checkbox-label'
                ]
            }))
        } else {
            <span class="nb-checkbox-flag ">
                 if .type == 'checkbox' {
                    @class += 'nb-checkbox-normal-flag'
                } else if .type {
                    @class += 'nb-checkbox-{.type}-flag'
                }
                <span class="nb-checkbox-flag-icon"></span>
            </span>

            <span class="nb-checkbox-label">
                if (.content) {
                    html(.content)
                }
            </span>
        }
    </label>
}
