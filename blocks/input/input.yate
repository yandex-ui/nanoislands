func nb-input(nodeset options) {
    default = {
        'size': 's'
    }

    if options.left || options.right || options.label || options.reset {
        nb-block('input', nb-extend(default, options))
    } else {
        nb-block('inputSimple', nb-extend(default, options))
    }
}

match .input nb {

    <label data-nb='input' class='nb-input nb-input_type_complex'>
        apply . nb-main-attrs
        if .size {
            @class += ' nb-input_size_{.size}'
        }

        if .disabled {
            @class += ' is-nb-disabled'
            @data-nb-disabled = 'true'
        }

        if .reset {
            <span class="nb-input__reset"></span>
        }

        <span class="nb-input__content">
            <input class="nb-input__controller" value="{.content}">
                for .attrs.* {
                    @{name()} = .
                }

                if .disabled {
                    @disabled = 'disabled'
                }
            </input>
            <span class="nb-input__view">&nbsp;</span>
        </span>
    </label>
}

match .inputSimple nb {
    <input data-nb='input' value="{.content}" data-nb-type='simple' class='nb-input _init nb-input_type_simple'>
        apply . nb-main-attrs

        if .size {
            @class += ' nb-input_size_{.size}'
        }

        if .disabled {
            @class += ' is-nb-disabled'
            @disabled = 'disabled'
            @data-nb-disabled = 'true'
        }
    </input>
}
