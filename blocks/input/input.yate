func nb-input(nodeset options) {
    default = {
        'size': 's'
    }

    if options.leftContent || options.rightContent || options.labelContent || options.reset || options.ghost || options.error || options.hint {
        if options.type == 'multiline' {
           _nb-block('textarea', _nb-extend(default, options))
        } else {
           _nb-block('input', _nb-extend(default, options))
        }
    } else {
        if options.type == 'multiline' {
            _nb-block('textareaSimple', _nb-extend(default, options))
        } else {
            _nb-block('inputSimple', _nb-extend(default, options))
        }
    }
}

match .input nb {

    errorId = 'nb-input_error' + _nb-uniq()

    <label>
        _size-theme-type-class({
           'name': 'input'
            if .size {
                'size': .size
            }
           'type': 'complex'
        })

        apply . nb-main-attrs

        if .hint || .error || .ghost {
            @class += ' _init'
        }

        if !( .static ){
            @data-nb='input'
        }

        if .disabled {
            @class += ' _nb-is-disabled'
            @data-nb-disabled = 'true'
        }

        if .ghost {
            @class += ' _nb-is-ghost'
            @data-nb-ghost = 'true'
        }

        if .error {
            @data-nb-error ='{{ "id": "{ errorId }", "direction":"{ .error.direction}"}}'
        }

        if .hint && .hint.text != '' {
            <span class="_nb-input-hint">
                <span class="_nb-input-hint-inner">
                    if .hint.ghost {
                        <span class="_nb-input-hint-ghost"></span>
                        <span class="_nb-input-hint-content">
                            .hint.text
                        </span>
                    } else {
                        .hint
                    }
                </span>
            </span>
        }

        if .error {
           nb-popup({
                "class": "_nb-error-popup"
                "content": .error.content
                "id": errorId
           })
        }

        if .reset {
            <span class="_nb-input-reset"></span>
        }

        if .leftContent {
            <span class="_nb-input-left">
                html(.leftContent)
            </span>
        }

        if .rightContent {
            <span class="_nb-input-right">
                html(.rightContent)
            </span>
        }

        <span class="_nb-input-content">
            <input class="_nb-input-controller" value="{.content}">
                for .attrs.* {
                    @{name()} = .
                }

                if .name {
                    @name = .name
                }

                if .disabled {
                    @disabled = 'disabled'
                }
            </input>
            <span class="_nb-input-view">&nbsp;</span>
        </span>
    </label>
}

match .textarea nb {

    errorId = 'nb-input_error' + _nb-uniq()

    <label>
        _size-theme-type-class({
           'name': 'input'
            if .size {
                'size': .size
            }
           'type': 'complex'
        })

        @class +=' _init'

        apply . nb-main-attrs

        if !( .static ){
            @data-nb='input'
        }

        if .disabled {
            @class += ' _nb-is-disabled'
            @data-nb-disabled = 'true'
        }

        if .ghost {
            @class += ' _nb-is-ghost _init'
            @data-nb-ghost = 'true'
        }

        if .error {
           @class += ' _init'
           @data-nb-error ='{{ "id": "{ errorId }", "direction":"{ .error.direction}" }}'

           nb-popup({
                "class": "_nb-error-popup"
                "content": .error.content
                "id": errorId
           })
        }

        if .reset {
            <span class="_nb-input-reset"></span>
        }

        if .leftContent {
            <span class="_nb-input-left">
                html(.leftContent)
            </span>
        }

        if .rightContent {
            <span class="_nb-input-right">
                html(.rightContent)
            </span>
        }

        <span class="_nb-input-content">
            <textarea class="_nb-input-controller">
                for .attrs.* {
                    @{name()} = .
                }

                if .name {
                    @name = .name
                }

                if .disabled {
                    @disabled = 'disabled'
                }

                if .content {
                    html(.content)
                }
            </textarea>
            <span class="_nb-input-view">&nbsp;</span>
        </span>
    </label>
}

match .inputSimple nb {
    <input value="{.content}" data-nb-type='simple'>
       _size-theme-type-class({
          'name': 'input'
           if .size {
               'size': .size
           }
          'type': 'simple'
       })
       @class += ' _init'

        apply . nb-main-attrs

        if .name {
            @name = .name
        }

        if !( .static ){
            @data-nb='input'
        }

        if .disabled {
            @class += ' _nb-is-disabled'
            @disabled = 'disabled'
            @data-nb-disabled = 'true'
        }
    </input>
}

match .textareaSimple nb {
    <textarea data-nb-type='simple'>
        _size-theme-type-class({
          'name': 'input'
           if .size {
               'size': .size
           }
          'type': 'simple'
        })
        @class += ' _init'

        apply . nb-main-attrs

        if .name {
            @name = .name
        }

        if !( .static ){
            @data-nb='input'
        }

        if .disabled {
            @class += ' _nb-is-disabled'
            @disabled = 'disabled'
            @data-nb-disabled = 'true'
        }

        if .content {
            html(.content)
        }

    </textarea>
}
