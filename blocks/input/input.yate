func nb-input(nodeset options) {
    default = {
        'size': 's'
    }

    if options.leftContent || options.rightContent || options.labelContent || options.reset || options.ghost || options.error || options.hint {
        if options.type == 'multiline' {
           nb-block('textarea', nb-extend(default, options))
        } else {
           nb-block('input', nb-extend(default, options))
        }
    } else {
        if options.type == 'multiline' {
            nb-block('textareaSimple', nb-extend(default, options))
        } else {
            nb-block('inputSimple', nb-extend(default, options))
        }
    }
}

match .input nb {

    errorId = 'nb-input_error' + nb-uniq()

    <label>
        _size-theme-type-class({
           'name': 'input'
            if .size {
                'size': .size
            }
           'type': 'complex'
        })

        @class +=' _init'

        apply . nb-main-attrs

        if .hint || .error || .ghost {
            @class += ' _init'
        }

        if !( .static ){
            @data-nb='input'
        }

        if .disabled {
            @class += ' nb-is-disabled'
            @data-nb-disabled = 'true'
        }

        if .ghost {
            @class += ' is-ghost'
            @data-nb-ghost = 'true'
        }

        if .error {
            @data-nb-error ='{{ "id": "{ errorId }", "direction":"{ .error.direction}"}}'
        }

        if .hint && .hint.text != '' {
            <span class="nb-input-hint">
                <span class="nb-input-hint-inner">
                    if .hint.ghost {
                        <span class="nb-input-hint-ghost"></span>
                        <span class="nb-input-hint-content">
                            .hint.text
                        </span>
                    } else {
                        .hint
                    }
                </span>
            </span>
        }

        if .error {
           nb-popup({
                "class": "nb-popup_type_error"
                "content": .error.content
                "id": errorId
           })
        }

        if .reset {
            <span class="nb-input-reset"></span>
        }

        if .leftContent {
            <span class="nb-input-left">
                html(.leftContent)
            </span>
        }

        if .rightContent {
            <span class="nb-input-right">
                html(.rightContent)
            </span>
        }

        <span class="nb-input-content">
            <input class="nb-input-controller" value="{.content}">
                for .attrs.* {
                    @{name()} = .
                }

                if .name {
                    @name = .name
                }

                if .disabled {
                    @disabled = 'disabled'
                }
            </input>
            <span class="nb-input-view">&nbsp;</span>
        </span>
    </label>
}

match .textarea nb {

    errorId = 'nb-input_error' + nb-uniq()

    <label>
        _size-theme-type-class({
           'name': 'input'
            if .size {
                'size': .size
            }
           'type': 'complex'
        })

        @class +=' _init'

        apply . nb-main-attrs

        if !( .static ){
            @data-nb='input'
        }

        if .disabled {
            @class += ' nb-is-disabled'
            @data-nb-disabled = 'true'
        }

        if .ghost {
            @class += ' is-ghost _init'
            @data-nb-ghost = 'true'
        }

        if .error {
           @class += ' _init'
           @data-nb-error ='{{ "id": "{ errorId }", "direction":"{ .error.direction}" }}'

           nb-popup({
                "class": "nb-popup_type_error"
                "content": .error.content
                "id": errorId
           })
        }

        if .reset {
            <span class="nb-input-reset"></span>
        }

        if .leftContent {
            <span class="nb-input-left">
                html(.leftContent)
            </span>
        }

        if .rightContent {
            <span class="nb-input-right">
                html(.rightContent)
            </span>
        }

        <span class="nb-input-content">
            <textarea class="nb-input-controller">
                for .attrs.* {
                    @{name()} = .
                }

                if .name {
                    @name = .name
                }

                if .disabled {
                    @disabled = 'disabled'
                }

                if .content {
                    html(.content)
                }
            </textarea>
            <span class="nb-input-view">&nbsp;</span>
        </span>
    </label>
}

match .inputSimple nb {
    <input value="{.content}" data-nb-type='simple'>
       _size-theme-type-class({
          'name': 'input'
           if .size {
               'size': .size
           }
          'type': 'simple'
       })
       @class += ' _init'

        apply . nb-main-attrs

        if .name {
            @name = .name
        }

        if !( .static ){
            @data-nb='input'
        }

        if .disabled {
            @class += ' nb-is-disabled'
            @disabled = 'disabled'
            @data-nb-disabled = 'true'
        }
    </input>
}

match .textareaSimple nb {
    <textarea data-nb-type='simple'>
        _size-theme-type-class({
          'name': 'input'
           if .size {
               'size': .size
           }
          'type': 'simple'
        })
        @class += ' _init'

        apply . nb-main-attrs

        if .name {
            @name = .name
        }

        if !( .static ){
            @data-nb='input'
        }

        if .disabled {
            @class += ' nb-is-disabled'
            @disabled = 'disabled'
            @data-nb-disabled = 'true'
        }

        if .content {
            html(.content)
        }

    </textarea>
}
