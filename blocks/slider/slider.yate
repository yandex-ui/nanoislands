func nb-slider(nodeset options) {
    default = {
        'value': 0
        'type': 'range'
        'size': 's'
        'orientation': 'horiz'
    }

    nb-block('slider', nb-deep-extend(default, options))
}

match .slider nb {
    <div class='nb-slider _init nb-slider_size_{ .size } nb-slider_orientation_{ .orientation }  nb-slider__handle_type_{ .type }'>
        apply . nb-main-attrs

        @data-nb-value = '{ .value }'

        if !.static {
            @data-nb = 'slider'
        }

        if .disabled {
            @class += ' nb-is-disabled'
        }

        <div class='nb-slider__body nb-slider__body_type_{ .type }'>
            <label class='ui-slider-handle'>
                if .size == 's' {
                    @class += ' nb-slider__small-handle'
                } else {
                    @class += ' nb-slider__handle'
                }

                if .handle.class {
                    @class += ' { .handle.class }'
                }

                <span class='nb-button-content'>
                    <input class='nb-slider__fallback' type='text' value='{ .value }' readonly='readonly'>
                        if .name {
                            @name = '{ .name }'
                        }
                    </input>
                </span>
            </label>
        </div>
    </div>
}
