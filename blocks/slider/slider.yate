func nb-slider(nodeset options) {
    default = {
        'value': 0
        'type': 'range'
        'size': 's'
        'orientation': 'horiz'
    }

    nb-block('slider', nb-deep-extend(default, options))
}

match .slider nb {
    <div>
        _size-theme-type-class({
            'name': 'slider'
            'size': .size
         })
        @class += " _init nb-{ .orientation }-slider nb-slider-{ .type }-handle"

        apply . nb-main-attrs

        @data-nb-value = '{ .value }'

        if !.static {
            @data-nb = 'slider'
        }

        if .disabled {
            @class += ' nb-is-disabled'
        }

        <div class='nb-slider-body nb-slider-{ .type }-body'>
            <label class='ui-slider-handle'>
                if .size == 's' {
                    @class += ' nb-slider-small-handle'
                } else {
                    @class += ' nb-slider-handle'
                }

                if .handle.class {
                    @class += ' { .handle.class }'
                }

                <span class='nb-button-content'>
                    <input class='nb-slider-fallback' type='text' value='{ .value }' readonly='readonly'>
                        if .name {
                            @name = '{ .name }'
                        }
                    </input>
                </span>
            </label>
        </div>
    </div>
}
